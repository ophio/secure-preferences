apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.novoda.bintray-release'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.3.0'
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 22
        versionCode libraryVersionCode
        versionName libraryVersionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile testLibraries.mockito
    testCompile testLibraries.jUnit

    androidTestCompile testLibraries.mockito
    androidTestCompile testLibraries.hamcrestLibrary
    androidTestCompile testLibraries.hamcrestCore

}


def siteUrl = 'https://github.com/ophio/secure-preferences'
def gitUrl = 'https://github.com/ophio/secure-preferences.git'
group = "in.co.ophio"


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'This library aims to encrypt the data in shared preferences. The encryption key is stored in ' +
                        'Android Keystore System. https://developer.android.com/training/articles/keystore.html.'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id System.getenv('BINTRAY_SECURE_PREF_USER_ID')
                        name System.getenv('BINTRAY_SECURE_PREF_USER_NAME')
                        email System.getenv('BINTRAY_SECURE_PREF_USER_EMAIL')
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

artifacts {

}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}


publish {
    userOrg = 'ophio'
    groupId = 'in.co.ophio'
    artifactId = 'secure-preferences'
    publishVersion = libraryVersionName
    desc = ''
    website = 'https://github.com/ophio/secure-preferences'
}

apply from: rootProject.file('checkstyle_android_library.gradle')